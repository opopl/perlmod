#!/usr/bin/env perl

use strict;
use warnings;

# при подключении Mojolicious::Lite автоматически подключаются warnings и strict
# но лучше перебдеть ;)
use warnings;
use strict;
use utf8;

use Mojolicious::Lite;
use Mojo::UserAgent;
use DBD::Pg;

$ENV{'MOJO_MODE'} = 'production';

get '/' => sub {
    my $self = shift;

    # адреса сайтов берутся из адресной строки, очистки нет поскольку
    # ссылки пойдут транзитом к поисковикам. При печати Mojolicious
    # по умолчанию экранирует символы < > & ' " против XSS-атак.
    my $sites = { map { lc $_, +{} } split /\r\n/, $self->param('site_url') };

    if ($sites) {
        # метод client устарел, нужно использовать метод ua,
        # или создать объект UserAgent явно и с нужными настройками.
        # устарел:  $self->client->get($g_req_url)->res->dom->at('div#resultStats');
        # работает: $self->ua->get($g_req_url)->res->dom->at('div#resultStats');
        # Создаем объект UserAgent
        my $ua = Mojo::UserAgent->new(
                    max_redirects   => 1,
                    name            => 'Mozilla/5.0 (Windows; Windows NT 6.1; rv:2.0)',
                 );

        foreach (keys %$sites) {
            # GOOGLE
            # запрашиваем страницу по адресу
            # "http://www.google.com/search?q=site%3A$site"
            # и из полученной страницы берем содержимое
            # блока <div id="resultStats">
            # at - ищет элемент по html-тегу
            my $g_req_url = 'http://www.google.ru/search?q=site%3A'.$_;
            $sites->{$_}{google}{req_url} = $g_req_url;
            $sites->{$_}{google}{result} =
                $ua->get($g_req_url)->res->dom->at('div#resultStats')->text =~
                    m/[\w:]+ \s ([\d\s]+)$/msx ? $1 : '---';


            # YANDEX
            # извлекает содержимое <strong class="b-head-logo__text">
            my $y_req_url = 'http://yandex.ru/yandsearch?text=site%3A'.$_.'&lr=2';
            $sites->{$_}{yandex}{req_url} = $y_req_url;
            $sites->{$_}{yandex}{result} =
                $ua->get($y_req_url)->res->dom->at('strong.b-head-logo__text')->text =~
                    m/^Наш(?:лось|ёлся) ([\d\w\s\.]+) \s \w+$/msx ? $1 : '---';
        }
    }

    $self->render(
        # название шаблона
        template    => 'index',
        format      => 'html',
        sites       => $sites,
    );
};

# Секретная фраза - используется для подписания кукисов,
# по умолчанию равна названию скрипта (имени приложения)
app->secret('P@r0L-dl!a_MoJ0!');
app->start('daemon');

# ниже в этом же файле объявляем шаблон index.html.ep
# index - название шаблона
# html - формат шаблона (кроме html - напр. text)
# ep - обработчик шаблона (от EmbeddedPerl, ещё - epl)
# название, формат и обработчик могут быть переданы методу render
# template => 'название', format => 'формат', handler => 'обработчик'

__DATA__

@@ index.html.ep
% layout 'default';
% title 'Mojolicious::Lite test';

<table cols="3" cellpadding="2" cellspacing="4" width="350px">
<thead>
<tr> <th colspan="3">Количество страниц в индексе поисковиков</th>
<tr> <th>Сайт</th><th>Google</th><th>Yandex</th> </tr>
</thead>
<tbody>
% foreach (sort keys %$sites) {
<tr>
    <td align="center"><%= $_ %></td>
    <td align="center">
        <a href="<%= $sites->{$_}->{google}->{req_url} %>">
        <%= $sites->{$_}->{google}->{result} %></a>
    </td>
    <td align="center">
        <a href="<%= $sites->{$_}->{yandex}->{req_url} %>">
        <%= $sites->{$_}->{yandex}->{result} %></a>
    </td>
</tr>
% }
</tbody>
</table>

<%# вспомогательная функция dumper - использует Data::Dumper %>
<%#= dumper($sites) %>
<hr>

<form action="./<%= $0 %>">
URL
<br />
  <textarea rows="5" cols="65" name="site_url">
   <%= join "\n", sort keys %$sites %>
  </textarea>
<br />
<input type="submit" />
</form>
<br />


@@ layouts/default.html.ep
<!doctype html><html>
  <head>
    <title><%= title %></title>
    <%= javascript ('http://dimio.org/cgi-bin/js/liveinternet-counter.js') %>
    <meta http-equiv="Content-Type" content="text/html; charset=utf-8" />
    <link rel="favicon" href="http://dimio.org/favicon.ico" type="image/x-icon" />
  </head>
  <body><%= content %></body>
</html>

